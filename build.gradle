plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "org.liquibase.gradle" version "2.0.4"
}

group = 'com.school'
version = '0.0.1'
sourceCompatibility = '14'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url "https://repo.spring.io/plugins-release" }
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    implementation "mysql:mysql-connector-java"
    implementation 'org.liquibase:liquibase-core'
    liquibaseRuntime "mysql:mysql-connector-java"
    liquibaseRuntime "org.liquibase:liquibase-core"
    liquibaseRuntime "org.liquibase.ext:liquibase-hibernate5:4.0.0"
    liquibaseRuntime sourceSets.main.compileClasspath
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    compile group: 'org.hibernate', name: 'hibernate-ehcache', version: '5.4.19.Final'
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'

    
}

test {
    jvmArgs(['--enable-preview'])
    useJUnitPlatform()
}


tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}


project.ext.diffChangelogFile = "src/main/resources/db/changelog/" + new Date().format("yyyyMMddHHmmss") + "_changelog.yaml"
if (!project.hasProperty("runList")) {
    project.ext.runList = "main"
}

liquibase {
    activities {
        main {
            driver "com.mysql.cj.jdbc.Driver"
            url "jdbc:mysql://localhost:3306/school_management?useUnicode=true&characterEncoding=utf8&useSSL=false&useLegacyDatetimeCode=false&serverTimezone=UTC&createDatabaseIfNotExist=true"
            username "root"
            password "root"
            changeLogFile "src/main/resources/db/liquibase/master.yaml"
            logLevel "debug"
            classpath "src/main/resources/"
        }
    }

    runList = project.ext.runList
}